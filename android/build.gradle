buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.1'
        classpath 'com.google.gms:google-services:4.4.0'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            if (project.android.hasProperty('namespace') == false || project.android.namespace == null) {
                // Intentar obtener el namespace del AndroidManifest.xml
                def manifestFile = project.file("src/main/AndroidManifest.xml")
                if (manifestFile.exists()) {
                    def manifestContent = manifestFile.text
                    def packageMatch = manifestContent =~ /package="([^"]+)"/
                    if (packageMatch.find()) {
                        project.android.namespace = packageMatch.group(1)
                    }
                }
            }
            
            // Forzar Java 17 para todos los subproyectos
            project.android.compileOptions {
                sourceCompatibility JavaVersion.VERSION_17
                targetCompatibility JavaVersion.VERSION_17
            }
            
            if (project.hasProperty('kotlin')) {
                project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
                    kotlinOptions {
                        jvmTarget = '17'
                    }
                }
            }
        }
    }
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
